# Maintainer: Rusty Bird <rustybird@net-c.com>
pkgname=( qubes-split-browser{,-disp} )
pkgver=$(cat version)
pkgrel=1
arch=( any )
license=( BSD )
url=https://github.com/rustybird/qubes-split-browser

package_qubes-split-browser() {
    pkgdesc='Split Browser for Qubes'
    depends=(
        awk
        bash
        coreutils
        dmenu
        libnotify
        python
        python-xcffib
        systemd
    )
    optdepends=(
        'pwgen: generate passwords'
        'oath-toolkit: authenticate by TOTP'
    )
    backup=(
        etc/split-browser/10-defaults.bash
        etc/split-browser/20-utf-8.bash.EXAMPLE
        etc/split-browser/prefs/11-no-updates.js
        etc/split-browser/prefs/12-no-tor-checks.js
        etc/split-browser/prefs/13-no-tor-daemon.js
        etc/split-browser/prefs/14-no-nags.js
        etc/split-browser/prefs/15-downloads-directory.js
        etc/split-browser/prefs/20-whonix-gateway.js
        etc/split-browser/prefs/32-ui-tweaks.js.EXAMPLE
    )

    make -C ../vm PREFIX=/usr DESTDIR="$pkgdir" install-persist
    install -D -m 644 -t "$pkgdir/usr/share/licenses/$pkgname/" ../LICENSE-0BSD
}

package_qubes-split-browser-disp() {
    pkgdesc='Split Browser for Qubes (DisposableVM side)'
    depends=(
        bash
        socat
        systemd
    )
    optdepends=( 'xdotool: autotype logins' )
    backup=(
        etc/split-browser-disp/10-defaults.bash
        etc/split-browser-disp/20-whonix-workstation.bash
    )

    make -C ../vm PREFIX=/usr DESTDIR="$pkgdir" install-disp
    install -D -m 644 -t "$pkgdir/usr/share/licenses/$pkgname/" ../LICENSE-0BSD
}
